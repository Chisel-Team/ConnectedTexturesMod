buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.parchmentmc.org' }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    	maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.ajoberstar:gradle-git:0.10.1'
    }
}
plugins {
    id 'maven-publish'
    id 'eclipse'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'io.freefair.lombok' version '8.1.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

import org.ajoberstar.grgit.Grgit

def repo = Grgit.open(project.file('.'))
project.ext.gitHash = repo.log().find().abbreviatedId

boolean dev = System.getenv('RELEASE') == null || System.getenv('RELEASE').equals('false');

ext.buildnumber = 0
project.buildnumber = System.getenv('BUILD_NUMBER') != null ? System.getenv('BUILD_NUMBER') : project.ext.gitHash

version = "${minecraft_version}-${mod_version}+" + project.buildnumber
group = "team.chisel.ctm" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = dev ? "CTM_DEV" : "CTM"
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: '2023.07.30-1.20.1'
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // default run configurations.
    // these can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            taskName 'ctm_client'
            workingDirectory project.file('run')

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            arg '-mixin.config=ctm.mixins.json'

            mods {
                ctm {
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }

        server {
            taskName 'ctm_server'
            workingDirectory project.file('run')

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            arg '-mixin.config=ctm.mixins.json'

            mods {
                ctm {
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }
    }
}

compileJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

// Workaround for SpongePowered/MixinGradle#38
afterEvaluate {
    tasks.configureReobfTaskForReobfJar.mustRunAfter(tasks.compileJava)
    tasks.configureReobfTaskForReobfJarJar.mustRunAfter(tasks.compileJava)
}

jar {
    manifest {
        attributes(["Specification-Title": "ctm",
                    "Specification-Vendor": "chisel-team",
                    "Specification-Version": "25.0", // We are version 1 of the modlauncher specification
                    "Implementation-Title": project.name,
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"chisel-team",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "MixinConfigs": "ctm.mixins.json"],)
    }
}

repositories {
    mavenLocal()
    maven {
        name = 'sponge'
    	url = 'https://repo.spongepowered.org/repository/maven-public/'
    	content {
    	    includeGroup "org.spongepowered"
    	}
    }
}

dependencies {
    minecraft 'net.neoforged:forge:' + minecraft_version + '-' + forge_version
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'

    annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT:processor'
}

test {
	useJUnitPlatform()
	maxHeapSize = '1G'
	testLogging {
		events "passed"
	}
}

lombok {
    version = "1.18.28"
}

// Create API jar with sources and compiled files
task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'team/chisel/ctm/api/**/*'
    archiveClassifier = 'api'
}

mixin {
    add sourceSets.main, "ctm.refmap.json"
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'CTM'
            artifact jar
            //artifact sourceJar
            artifact apiJar
        }
    }

    repositories {
        if (project.hasProperty('mavendir')) {
            maven { url mavendir }
        }
    }
}

String getChangelogText() {
    def changelogFile = file('changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            if (lineCount == 0) {
                temp = "CTM ${version}"
                temp = "<h2>$temp</h2>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp"
                temp = temp.replaceAll("(\\S+\\/\\S+)#([0-9]+)\\b", "<a href=\"https://github.com/\$1/issues/\$2\">\$0</a>");
                temp = temp.replaceAll("#([0-9]+)\\b(?!<\\/a>)", "<a href=\"https://github.com/$github_project/issues/\$1\">\$0</a>");
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += "$temp<br/>"
            lineCount++
            return
        } else {
            done = true
        }
    }
    return str
}

tasks.curseforge.enabled = !dev && project.hasProperty('curseforge_key')
    
curseforge {
    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }

    project {
        id = project.projectId
        changelog = System.getenv('CHANGELOG') == null || System.getenv('CHANGELOG').equals('none') ? getChangelogText() : System.getenv('CHANGELOG')
        changelogType = 'html'
        releaseType = project.curse_type
        mainArtifact(jar) {
            displayName = "CTM - ${version}"
        }
        addGameVersion project.minecraft_version
    }
}
